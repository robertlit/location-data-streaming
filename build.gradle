plugins {
    id 'java'
    id 'application'
    id 'com.google.protobuf' version '0.9.4'
}

group = 'me.robertlit'
version = '1.0-SNAPSHOT'

mainClassName = 'me.robertlit.datastream.LocationEventServer'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    runtimeOnly 'io.grpc:grpc-netty-shaded:1.57.2'
    implementation 'io.grpc:grpc-protobuf:1.57.2'
    implementation 'io.grpc:grpc-stub:1.57.2'
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53'

    implementation 'org.jetbrains:annotations:24.0.0'
}

test {
    useJUnitPlatform()
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.22.3"
    }

    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.57.2'
        }
    }

    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

run {
    jvmArgs = ['--add-exports=java.base/sun.nio.ch=ALL-UNNAMED']
}
